plugins {
    id 'java-platform'
    id 'maven-publish'
    id 'signing'
    id 'net.researchgate.release' version '2.8.1'
}

ext {
    // Project constants
    github_org = 'Discord4J'
    project_name = 'Discord4J'
    artifact_group = 'com.discord4j'
    project_version = "$version".toString()
    project_description = 'Bill of Materials (BOM) for Discord4J'
    project_url = 'https://discord4j.com'

    reactor_bom_version = 'Dysprosium-SR7'

    isRelease = !version.toString().endsWith('-SNAPSHOT')
}

group = artifact_group
version = project_version
description = project_description

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/staging' }
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

javaPlatform.allowDependencies()

dependencies {
    api platform("io.projectreactor:reactor-bom:$reactor_bom_version")
    constraints {
        // standard style
        api "com.discord4j:discord4j-common:$discord4jVersion"
        api "com.discord4j:discord4j-core:$discord4jVersion"
        api "com.discord4j:discord4j-gateway:$discord4jVersion"
        api "com.discord4j:discord4j-rest:$discord4jVersion"
        api "com.discord4j:discord4j-voice:$discord4jVersion"
        api "com.discord4j:stores-api:$storesVersion"
        api "com.discord4j:stores-common:$storesVersion"
        api "com.discord4j:stores-caffeine:$storesVersion"
        api "com.discord4j:stores-jdk:$storesVersion"
        api "com.discord4j:stores-redis:$storesVersion"
        api "com.discord4j:stores-tck:$storesVersion"
        api "com.discord4j:connect-common:$connectVersion"
        api "com.discord4j:connect-kafka:$connectVersion"
        api "com.discord4j:connect-rsocket:$connectVersion"
        // jitpack style
        api "com.discord4j.discord4j:discord4j-common:$discord4jJitpackVersion"
        api "com.discord4j.discord4j:discord4j-core:$discord4jJitpackVersion"
        api "com.discord4j.discord4j:discord4j-gateway:$discord4jJitpackVersion"
        api "com.discord4j.discord4j:discord4j-rest:$discord4jJitpackVersion"
        api "com.discord4j.discord4j:discord4j-voice:$discord4jJitpackVersion"
        api "com.discord4j.Stores:stores-api:$storesJitpackVersion"
        api "com.discord4j.Stores:stores-common:$storesJitpackVersion"
        api "com.discord4j.Stores:stores-caffeine:$storesJitpackVersion"
        api "com.discord4j.Stores:stores-jdk:$storesJitpackVersion"
        api "com.discord4j.Stores:stores-redis:$storesJitpackVersion"
        api "com.discord4j.Stores:stores-tck:$storesJitpackVersion"
        api "com.discord4j.connect:connect-common:$connectJitpackVersion"
        api "com.discord4j.connect:connect-kafka:$connectJitpackVersion"
        api "com.discord4j.connect:connect-rsocket:$connectJitpackVersion"
        // transitives
        api "io.lettuce:lettuce-core:5.3.0.RELEASE"
        api "com.discord4j:discord-json:1.3.0-SNAPSHOT"
        api "io.rsocket:rsocket-core:1.0.0-RC7"
        api "io.rsocket:rsocket-transport-netty:1.0.0-RC7"
        api "io.netty:netty-common:4.1.49.Final"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'bom'
            from components.javaPlatform
            pom {
                name = 'BOM'
                description = 'Bill of Materials (BOM) for Discord4J'
                url = 'https://github.com/Discord4J/BOM'
                organization {
                    name = 'Discord4J'
                    url = 'https://discord4j.com'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/Discord4J/BOM/issues'
                }
                licenses {
                    license {
                        name = 'LGPL-3.0'
                        url = 'https://github.com/Discord4J/BOM/LICENSE.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/Discord4J/BOM'
                    connection = 'scm:git:git://github.com/Discord4J/BOM.git'
                    developerConnection = 'scm:git:git://git@github.com:Discord4J/BOM.git'
                }
                developers {
                    developer {
                        name = 'The Discord4J Team'
                    }
                }
            }
            versionMapping {
                usage('java-api') {
                    fromResolutionResult()
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        maven {
            if (isRelease) {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            } else {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            }
            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                credentials {
                    username project.property('sonatypeUsername')
                    password project.property('sonatypePassword')
                }
            }
        }
    }
}

if (isRelease) {
    signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        if (signingKey != null && signingPassword != null) {
            useInMemoryPgpKeys(new String(signingKey.decodeBase64()), signingPassword as String)
        }
        sign publishing.publications.mavenJava
    }
}

release {
    preTagCommitMessage = 'Release version'
    tagCommitMessage = 'Release version'
    newVersionCommitMessage = 'Next development version'
    git {
        requireBranch = ''
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
